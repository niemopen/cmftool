plugins {
    id 'application'
    id 'distribution'
    //id 'org.cyclonedx.bom' version '3.0.0-alpha-1'
}

repositories {
    mavenCentral()
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

dependencies {
    implementation project(':lib-cmf')
    implementation project(':lib-util')
    implementation 'org.jcommander:jcommander:2.0'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'
    implementation 'com.google.code.gson:gson:2.13.1'

    testImplementation libs.junit.jupiter
    testImplementation 'org.assertj:assertj-core:3.26.0'
    testImplementation 'io.github.hakky54:logcaptor:2.9.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    applicationName = 'niemtran'
    mainClass = 'org.mitre.niem.translate.NIEMTran'
}

distributions {
    main {
        distributionBaseName = 'niemtran'
    }
}



jar {
    manifest {
        attributes 'Implementation-Version': String.format('%s (%tF)', project.version, java.time.LocalDateTime.now()),
                   'Implementation-Title': 'NIEMTran'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
